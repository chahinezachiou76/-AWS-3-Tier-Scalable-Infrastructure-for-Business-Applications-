# External ALB SG

resource "aws_security_group" "external_alb_sg" {
  name        = "multi-az-external-alb-sg"
  description = "Allow internet traffic to external ALB"
  vpc_id      = var.vpc_id

  ingress {
    description = "HTTP"
    from_port   = 80
    to_port     = 80
    protocol    = "tcp"
    cidr_blocks = ["0.0.0.0/0"]
  }

  ingress {
    description = "HTTPS"
    from_port   = 443
    to_port     = 443
    protocol    = "tcp"
    cidr_blocks = ["0.0.0.0/0"]
  }

  egress {
    from_port   = 0
    to_port     = 0
    protocol    = "-1"
    cidr_blocks = ["0.0.0.0/0"]
  }

  tags = merge(var.tags, {
    Name = "multi-az-external-alb-sg"
  })
}


# Frontend EC2 SG

resource "aws_security_group" "frontend_sg" {
  name        = "multi-az-frontend-sg"
  description = "Allow traffic from external ALB"
  vpc_id      = var.vpc_id

  ingress {
    from_port       = 80
    to_port         = 80
    protocol        = "tcp"
    security_groups = [aws_security_group.external_alb_sg.id]
  }

  egress {
    from_port   = 0
    to_port     = 0
    protocol    = "-1"
    cidr_blocks = ["0.0.0.0/0"]
  }

  tags = merge(var.tags, {
    Name = "multi-az-frontend-sg"
  })
}


# Internal ALB SG

resource "aws_security_group" "internal_alb_sg" {
  name        = "multi-az-internal-alb-sg"
  description = "Allow traffic from frontend"
  vpc_id      = var.vpc_id

  ingress {
    from_port       = 80
    to_port         = 80
    protocol        = "tcp"
    security_groups = [aws_security_group.frontend_sg.id]
  }

  egress {
    from_port   = 0
    to_port     = 0
    protocol    = "-1"
    cidr_blocks = ["0.0.0.0/0"]
  }

  tags = merge(var.tags, {
    Name = "multi-az-internal-alb-sg"
  })
}


# Backend EC2 SG

resource "aws_security_group" "backend_sg" {
  name        = "multi-az-backend-sg"
  description = "Allow traffic from internal ALB"
  vpc_id      = var.vpc_id

  ingress {
    from_port       = 8080
    to_port         = 8080
    protocol        = "tcp"
    security_groups = [aws_security_group.internal_alb_sg.id]
  }

  egress {
    from_port   = 0
    to_port     = 0
    protocol    = "-1"
    cidr_blocks = ["0.0.0.0/0"]
  }

  tags = merge(var.tags, {
    Name = "multi-az-backend-sg"
  })
}

# Aurora DB SG

resource "aws_security_group" "database_sg" {
  name        = "multi-az-database-sg"
  description = "Allow DB access from backend"
  vpc_id      = var.vpc_id

  ingress {
    from_port       = 3306
    to_port         = 3306
    protocol        = "tcp"
    security_groups = [aws_security_group.backend_sg.id]
  }

  egress {
    from_port   = 0
    to_port     = 0
    protocol    = "-1"
    cidr_blocks = ["0.0.0.0/0"]
  }

  tags = merge(var.tags, {
    Name = "multi-az-database-sg"
  })
}


# Bastion SG

resource "aws_security_group" "bastion_sg" {
  name        = "multi-az-bastion-sg"
  description = "SSH access from admin IP"
  vpc_id      = var.vpc_id

  ingress {
    from_port   = 22
    to_port     = 22
    protocol    = "tcp"
    cidr_blocks = var.admin_cidr_blocks
  }

  egress {
    from_port   = 0
    to_port     = 0
    protocol    = "-1"
    cidr_blocks = ["0.0.0.0/0"]
  }

  tags = merge(var.tags, {
    Name = "multi-az-bastion-sg"
  })
}
